#lang riposte

%base := http://localhost:12345

$uuid := @UUID with fallback "daf10488-4e6d-4bd5-98b6-cd926dc41e10"

# -----------------------
# Define a UUID and start
# sending requests!
# -----------------------

GET cart/{uuid} responds with 2XX

/grand_total exists

$productId := 41966
$qty := 5
$campaignId := 1

$payload := {
  "product_id": $productId,   # we extend the JSON syntax here
  "campaign_id": $campaignId, # in that you can use Riposte variables
  "qty": $qty                 # and you can add comments to JSON, too
}

POST $payload to cart/{uuid}/items responds with 200

# extract the item ID and check that it has a certain kind of value

$itemId := /items/0/cart_item_id (positive integer)
